label scene2_X:
    pause 0.3
    stop music
    call hide_menu

    scene fragplane with ImageDissolve("diagonal_crop.png", 2, ramplen=4)

    pause 2

    play sound "audio/sfx/teleport.wav"
    show hnb smug2 nopper with squares
    hb "Che cringiata!"

    hb "Ancora a ripetere le stesse patetiche scuse. Pensa di meritare la vittoria dopo quello che ha fatto."

    show hnb fury fist
    hb "Pagherà con la distruzione di tutto ciò che ha costruito."

    n "In quel momento, sembrava che tutto stesse andando secondo i piani di Hanabi..."

    play music "audio/umi/fishy aroma.mp3" volume 1.2
    show hnb yep smug -fist
    hb "Già, tutto secondo i miei piani."

    n "Non è stato difficile per lui, d'altronde tutto quello che ha dovuto fare è ingannare Check, non un grande ostacolo dal momento che si inganna da solo."

    hb "Puoi dirlo forte!"

    n "Un lavoro facile, ma ben riuscito. Non gli resta che far scadere il tempo e tornare da Lambdadelta vittorioso."

    show hnb nope napalm_right at flip
    hb "Mmhh?"

    n "Ma stranamente, mentre si crogiolava nella sua gloria, cominciò a sentire delle strane voci."
    show hnb -napalm_right at unflip

    hb "Ehi in che senso..."
    play sound "audio/sfx/teleport.wav"
    show hnb:
        ease 1 xalign 0.9

    show fragplane greenflare with Dissolve(2)

    pause 1
    cr "Congratulazioni... Un piano ben riuscito, giusto? Una vittoria meritata."

    hb "Chi diamine è adesso? Sei qui per ribadire l'ovvio? Fatti vedere!"

    hd "Questa è una richiesta che non possiamo esaudire. {w}Per spiegarlo in modo che anche un principiante possa capire... ti stiamo parlando da dietro i punti ciechi di questo frammento. {w}Uscire allo scoperto sarebbe un rischio che non siamo disposti a correre."

    cr "Siamo solo venuti per farti I complimenti! Non badare a noi, non abbiamo alcuna intenzione, men che meno la capacità, di interferire in quello che stai facendo."

    hd "Apprezzeremmo se pensassi a noi come semplici voci nella tua testa. {w} O se preferisci delle allucinazioni."

    show hnb fury
    cr "Però vorrei chiederti... Cosa è la vittoria per te? Cosa significa vincere il gioco?"

    hd "Stiamo solo controllando se le regole ti sono chiare, non ti offendere."

    hb "Che domande... La mia vittoria è la sconfitta di Check. Non voglio nient'altro."



    show hnb smug2

    hd "Chiaro, chiaro... bene, abbiamo avuto la nostra risposta. Anche se devo dire, mi sembri un po' troppo rilassato."

    hb "Che intendi, stai cercando di confondermi?"

    hd "Certo che no... ma anche se tu stai vincendo in maniera schiacciante, c'è sempre una possibilità, per quanto infinitesimale, che Check possa risolvere il mistero... Ed in quel caso sai cosa farebbe di te Lambdadelta, non è vero?"

    show hnb grin smug
    hb "Non perderò! Non può battermi come è adesso, quindi non c'è niente da discutere."

    cr "Questo è lo spirito giusto! Ma se ti dicessi che, c'è un modo per scampare alle conseguenze? In caso un miracolo accadesse davvero."

    show hnb smug nope
    hb "Se avete qualcosa da dire smettete di girarci intorno e venite al punto."

    cr "Nonostante i suoi incredibili poteri, Lambdadelta non è completamente onnipotente."

    hd "Questo è un gioco tra te e Check, non più il suo. Siete fuori dalla sua giurisdizione, il suo campo di influenza."

    hd "Il suo obiettivo era allontanare Check, ma nel farlo ha creato una situazione dove qualunque cosa succeda non potrà agire subito."

    cr "Insomma, i suoi poteri quì non si applicano. Finchè non torni sul suo stesso livello non può farti niente. Capisci cosa voglio dire?"

    show hnb grin
    hb "Non c'è niente da capire, non ho alcuna possibilità di sconfitta! Hananananana!"

    hd "Molto bene. Non abbiamo intenzione di abusare ulteriormente del tua pazienza."

    cr "Arrivederci. Che I tuoi risultati siano proporzionali al tuo impegno."

    play sound "audio/sfx/teleport.wav"

    show fragplane
    with Dissolve(2)


    show hnb yep:
        linear 1 center

    pause 1

    hb "Chi credono di essere, ho tutto sotto controllo......."

    show hnb nope
    hb "Mmh? No davvero, chi diamine erano?"

    show hnb fury fist
    hb "Ci penserò con calma quando avrò distrutto Check."

    window hide

    stop music fadeout 5
    show hnb:
        easein 1.5 offscreenleft

    pause 5

    play music "audio/higu/dancers5.mp3" fadein 4
    show check think nope sor p:
        offscreenright
        easein 2 center
    pause 2


    ck "Pensa Check pensa. Deve esserci qualcosa che ti sfugge."

    n "In quel momento, mentre Check era profondamente assorto nei suoi ragionamenti, fu un ricordo nostalgico a distrarlo."

    show check sus yep

    ck "Se solo Larry fosse quì... {w}Avrei accesso a tutte le informazioni importanti invece di affidarmi alla memoria."

    n "Ritrovandosi di nuovo a lavorare da solo, Check si rese conto per la prima volta quanto nonostante tutto la presenza del suo giovane assistente semplificasse qualsiasi indagine."

    ck "Spero non se la stia passando troppo male. {w}Quando tornerò devo ricordare a me stesso di dargli un aumento."

    show check:
        ease 1 left
    pause 1

    show fragplane orangeflare
    show taccuino_prop:
        anchor (0.5,0.5)
        pos (770,440)
        zoom 0.1

        parallel:
            linear 1 zoom 1

        parallel:
            easein 1 pos (0.7,0.5)

    pause 1
    show fragplane
    pause 1
    show check angry nope fp at flip
    pause 0.5

    show check:
        linear 1 center

    pause 2

    ck "Cosa? Un taccuino quì?"

    show taccuino_prop at flip:
        ease 0.5 align (0.65, 0.3)

    ck"......{w} Ma questo è... identico a quello di Larry! {w}Anche la calligrafia, non c'è dubbio, è la sua."

    show check sus
    ck "Non ho la minima idea di come ha fatto a finire in questo posto{w}, ma se alle informazioni già presenti aggiungessi quelle che ho scoperto...."

    show check -sus smile
    ck "Forse ho ancora una possibilità."


    play sound "audio/sfx/secret_unlock.mp3"

    n "Check ha sbloccato: {color=#780000}{b}Taccuino delle indagini{/b}{/color}."



    stop music fadeout 5
    hide taccuino_prop
    show check:
        easein 1.5 offscreenright

    pause 4

    play sound "audio/sfx/teleport.wav"

    show fragplane greenflare with Dissolve(2)

    cr "Sicuro? Non è un po'troppo?"

    hd "Non ti preoccupare, non verremo scoperti per così poco."

    cr "Non parlo di quello, lo sai."

    hd "È importante anche per noi che si dia una svegliata il prima possibile... E poi, non è che gli stiamo dando una soluzione."

    hd "Non capisco proprio perchè dovresti obiettare."

    cr "Mmmmhhhhh. Anche questo risvolto sembra interessante. {w}Va bene, te l'approvo. {p}Questa volta."



    stop music fadeout 8
    scene black with longFade
    $persistent.notes = True
    jump mid_start

    return